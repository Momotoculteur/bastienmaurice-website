---
date: 2023-08-11
authors: [bmaurice]
title: Les bonnes pratiques pour l'initialisation d'un nouveau projet Terraform (tf.state)
description: >
  test maurice
categories:
  - Terraform
tags:
  - Terraform
  - Infra as code
  - State
  - Remote backend
  - Provider
comments: true
---

# Les bonnes pratiques pour l'initialisation d'un nouveau projet Terraform (tf.state)

Je te montre les best practices pour ton Terraform State, en minimisant les erreurs manuelles, le leak de secrets, la gestion du locking. Bref, comment g√©rer le partage du tf.state dans une √©quiep de d√©v.  
Cas concret avec un remote backend sur du AWS Bucket S3 et DynamoDB.

<!-- more -->
<br>

J'ai souhait√© r√©cemment me faire un petit blog sur le net et l'heberger sur du AWS. J'ai voulu faire les choses propres et me faire mon infra as code avec Terraform. Mais avant de pouvoir plan et apply mes nouvelles ressources dont j'ai besoin, j'ai du me confront√© √† l'initialisation d'un projet Terraform.

<br>
Et si tu veux faire les choses bien, je peux te montrer quelques good practices concernant la gestion de ton tf.state ;)


Je te montre √ßa sur du AWS, mais tu peux faire exactement la m√™me chose sur les autres clouds provider. Seul le noms des ressources vont changer de l'un √† l'autre !

## Pr√©-requis
Tools :   

- Terraform

## tf.state
### C'est quoi ?
Quand tu vas d√©finir la cr√©ation, la mise √† jour ou la suppression de ressource via des fichiers Terraform, lui m√™me doit savoir dans quel ordre r√©aliser ces instructions. Les ressources ayant des specifications diff√©rentes, et des d√©p√©nendances entre elle plus ou moins forte, Terraform va se faire un bon gros fichier JSON resencent l'ensemble de ces instructions dans le bon ordre.

### Quels sont les soucis de ce tf.state ?
Aucun pour une utilisation simple. Realisant moi m√™me en solo ce blog, je peux tr√®s bien faire un simple terraform init et commencer √† d√©v mon infra sans soucis, le tout en local. Mais quid sur un vrai projet avec plusieurs dev travaillant ensemble ? C'est l√† ou les choses vont se compliquer.

- Partager le tf.state √† plusieurs : Si chacun des membres de l'√©quipe doit mettre √† jour cet infra, ils doivent pouvoir avoir librement acc√®s √† ce fichier
- Verouiller le tf.state : Soucis de bosser √† plusieurs, c'est que ce fichier ne doit √™tre utiliser que par une seule et unique personne sur un instant T. Si tu essayes de faire un acc√®s concurrent √† ce fichier tu peux finir avec une corruption du tf.state et mettre en peril ton infra
- Isoler le tf.state : Plus ton infra grandi, plus les plan/apply risque d'√™tre long. Isoler en une multitude de plus petits tf.state te permet de travailler plus vite, et surtout en cas de soucis de ne pas casser toute ton infra.

### Comment partager ce tf.state ? 
#### Git ? Non !

Tu le sais d√©j√†, mais on va √™tre obliger √† un moment donn√© d'utiliser Git. Mais cela n'est pas possible car cela engendre plusieurs probl√®mes :

- Erreur manuelle: on oublie trop souvent de pull le latest change sur ton repo Git, √ßa peut donc t'amener tr√®s rapidement √† des r√©gr√©ssions d'infra
- Verouillage: Impossible de verouiller un fichier √† un acc√®s concurrentiel, qui emp√™cherais donc 2 membres d'une m√™me √©quipe d'y acc√©der au m√™me moment
- Secret: Et enfin le plus important, c'est que ce fichier tf.state contient tout les infos de ton infra en texte clair. Ce qui signifie que tout secret, token, ou donn√©es sensible peut √™tre leak sur ton git.

#### Remote backend ? OUI !
C'est le feu car √ßa r√©solve les 3 probl√©matiques que je viens de vous exposer :

- Erreur manuelle: Terraform va automatiquement charger le tf.state a chaque fois que tu effecue un terraform plan ou apply, et automatiquement renvoyer sur le remote backend le nouveau tf.state fraichement cr√©e
- Verouillage: la plupart des remotes backend supportent nativement le locking. Et si quelqu'un d'autre √† aqu√©ri le lock √† un instant T, tu vas devoir attendre qu'il ait fini. Il existe une commande, plut√¥t cet argument ci: *-lock-timeout=<temps>* qui te permet de dire √† terraform d'attendre jusqu'a cette p√©riode l√† pour effectuer ta commande
- Secret: La plupart des remotes backend supportent aussi nativement l'encryptage, de configurer les permissions d'acc√®s et donc de d√©finir quel utilisateur √† le droit

#### Exemple avec un AWS S3 bucket
Dans mon cas on va faire avec un service manag√© par AWS qui te rend ultra simple cet ajout l√†, le S3. Qu'apporte t-il ?

- Service manag√©, donc ballek de maintenir une infra suppl√©mentaire pour ce cas d'usage
- Niveau durabilit√© et disponibilit√©, on est √† 99,99.....%, donc tu crains pas d'avoir des soucis pour y acceder √† tout heure
- Support l'encryptage, ce qui est plut√¥t cool avec les donn√©es sensibles stock√© dedans le tf.state. Example avec de l'AES-256 niveau encryption c√¥t√© Amazon, et on utilise du TLS pour la communication AWS <-> Terraform
- Verouillage avec du DynamoDB
- Versionnage: te permet de d√©finir des incrementations de versions du tf.state, pratique donc de rollback sur une ancienne version si tu casses ton infra !
- Pas bien cher, tu va fit avec le free tier si tu veux tester pour t'entrainer !

## Let's code !
### D√©finition du provider
On commence par ajouter le provider : 

```terraform linenums="1"
# provider.tf
provider {
    region = "eu-west-3"
}
```

!!! note "Authentication du provider"
        Je le montre pas ici, mais tu dois permettre √† Terraform de s'authentifier √† ta console de management AWS. C'est pas la best practices, mais ici j'ai d√©fini un **access_id** et **secret_id** g√©n√©r√© depuis mon user root d'AWS dans un *./aws/* folder. Je ne d√©fini pas de *profil_name* en particulier dans mon *provider*, donc il prend le default de base.

### Cr√©ation du S3 Bucket
On d√©fini notre S3 bucket. Il va nous permettre de garder sur disque notre tf.state.

```terraform linenums="1"
# state_s3.tf
resource "aws_s3_bucket" "s3_state" {
  bucket = local.bucket_state_name
  tags   = local.commonTags

  lifecycle {
    prevent_destroy = true
  }
}
```

!!! tips "lifecycle"
        Je rajoute un hook ici, une s√©curit√© si je fais par m√©garde un **terraform destroy**. Sans trop faire gaffe je pourrais delete le contenu de tout le bucket. Ici on n'emp√™che d'apply le destroy

<br>
J'ai utilis√© des variables locales dans le code pr√©c√©dent, rien de particulier ici √† pr√©ciser mais je te donne mes valeurs √† titre d'example:
```terraform linenums="1"
# locals.tf
locals {
  bucket_state_name   = "bastienmaurice-website-infra-state"
  dynamodb_state_name = "bastienmaurice-website-infra-state"
}
```

### Ajout du versionning
On active le versionning sur notre bucket. Cela permet de g√©n√©rer un nouveau folder sur notre bucket pour chaque nouveau tf.state que l'on pousse, suite √† une mise √† jour de notre infrastructure. Pratique en cas de rollback √† une version ant√©rieur, si on casse tout √† un certains moment ! üòâ
```terraform linenums="1"
# state_s3.tf
resource "aws_s3_bucket_versioning" "s3_versionning" {
  bucket = aws_s3_bucket.s3_state.id

  versioning_configuration {
    status = "Enabled"
  }
}
```

### Activation de l'encryptage
Histoire d'augmenter le niveau de s√©curit√©, on encrypte le contenu du bucket S3. Super important, car tout token, secret et autre donn√©e sensible se retrouve en plain text sur le tf.state. Bonjour le leak si une persionne malveillante arrive sur ton bucket ! üòÖ

```terraform linenums="1"
# state_s3.tf
resource "aws_s3_bucket_server_side_encryption_configuration" "s3_encryption" {
  bucket = aws_s3_bucket.s3_state.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}
```

### Rendre le S3 priv√©
On va rendre le bucket en mode priv√©e afin d'√©viter que de mauvaises personnes mal-intention√© r√©cup des infos sensible :
```terraform linenums="1"
# state_s3.tf
resource "aws_s3_bucket_public_access_block" "s3_access" {
  bucket = aws_s3_bucket.s3_state.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}
```

### Gestion du locking
On ajoute une table sous DynamoDB pour g√©rer le v√©rouillage du tf.state. C'est une DB g√©r√© par Amazon en mode distribu√© pour des paires cl√©-valeur. Une sorte de redis qui devrait d'avantage te parler.
```terraform linenums="1"
# state_dynamobdb.tf
resource "aws_dynamodb_table" "tf_lock" {
    name = local.dynamodb_state_name
    tags = local.commonTags
    
    billing_mode = "PAY_PER_REQUEST"
    hash_key = "LockID"
    attribute {
        name = "LockID"
        type = "S"
    }
}
```

### Initialisation de l'infra - part1
A partir de l√†, tu peux commencer √† faire un **terraform init**, puis **terraform plan** et enfin **terraform apply**. Tu vas avoir ton S3 Bucket et ta table sous DynamoDB de cr√©es. 

!!! question
        Bizarre, je ne vois aucun tf.state dans mon bucket qui d√©fini mon infra actuelle

<br>
Et c'est normal ma gueule. On √† encore la sauvegarde du tf.state en local, vu que ton provider initiallement cr√©e au d√©but du tutoriel target ton local.
<br>

#### From Local Backend to Remote Backend
On touche du doigt la fin du chapitre. C'est ici que l'on va modifier notre configuration de notre provider, afin de lui dire d'aller target le backend disponible en ligne sur notre S3

```terraform linenums="1"
# providers.tf
terraform {
  required_providers { # Nouveau bloc
    aws = {
      source  = "hashicorp/aws"
      version = "5.11.0"
    }
  }

  backend "s3" { # Nouveau bloc
    bucket = "bastienmaurice-website-infra-state"
    key    = "terraform/state/terraform.tfstate"
    region = "eu-west-3"

    dynamodb_table = "bastienmaurice-website-infra-state"
    encrypt        = true
  }
}
```

- **required_providers**: un bloc permettant de d√©finir une version du prodiver que tu uitlises, tr√®s utile si tu bosses sur la m√™me infra avec plusieurs d√©v dessus.  √ßa t'√©vitera des soucis de compatibilit√© et avoir une version commun  
- **backend**: C'est ici que l'on target notre S3 fraichement cr√©e. On y renseigne le nom de notre bucket qui contient notre tf.state, et aussi notre DynamoDB qui va g√©rer le locking de ce fichier, permettant les acc√®s concurentiel et emp√™cher toute corruption de celui-ci

### Initialisation de l'infra - part2
Tu vas pouvoir refaire un second *terraform init*. A partir de l√† terraform va te dire que tu as d√©j√† un tf.state en local, et va te demander si tu souhaites desormais l'avoir sur ton s3 en partant de ton actuel. Tape *yes* et Finito, tu as bien ton tf.state sur AWS et qui suit les bonnes pratiques :)


## Conclusion & r√©sum√©
Comme tu as pu le voir, tu as quelques manips √† faire : 
1.  Ecrire une premi√®re partie de l'infra sous terraform pour cr√©er ton bucket S3 et ta table sous DynamoDB avec un tf.state en local
2. Ajouter un remote backend et le configure pour qu'il puisse utiliser le S3 et ta DB pr√©c√©dement cr√©e, et refaire un *terraform init* pour copier ton tf.state local que tu as d√©j√† utilis√©.

<br>
Petit tips, si tu veux supprimer ton infra existante, t√¢che de faire ces √©tapes √† l'envers ; √† savoir :

1. Supprimer le remote backend de la configuration terraform, et re-run le *terraform init* pour avoir ton tf.state en local
2. Premier terraform init/plan/apply.
3. Lance ton *terraform destroy* pour supprimer ton bucket S3 et ton DynamoDB
4. Second terraform init/plan/apply, avec migration de ton state local courant, vers le remote.

